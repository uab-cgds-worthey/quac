'''
USAGE:
module reset
module load Anaconda3/2020.02
module load snakemake/5.9.1-foss-2018b-Python-3.6.6

snakemake -s workflow/Snakefile --use-conda -k -p
'''
from pathlib import Path
import pandas as pd

WORKFLOW_PATH = Path(workflow.basedir).parent
configfile: WORKFLOW_PATH / "configs/workflow.yaml"


def get_samples(fpath):

    samples = ()
    with open(fpath, 'r') as f_handle:
        for line in f_handle:
            if line.startswith('#'):
                continue
            sample = line.split('\t')[1]
            samples += (sample,)

    return samples

EXTERNAL_DIR = Path("data/external")
RAW_DIR = Path("data/raw")
INTERIM_DIR = Path("data/interim")
PROCESSED_DIR = Path("data/processed")

LOGS_PATH = Path(config['logs_path'])
LOGS_PATH.mkdir(parents=True, exist_ok=True)
PROJECTS_PATH = Path(config['projects_path'])
PROJECT_NAMES = config['project_names'].split(',')
SAMPLES = {project: get_samples(RAW_DIR / f"ped/{project}.ped") for project in PROJECT_NAMES}


def get_targets(tool_name, projects=PROJECT_NAMES, sample_dict=SAMPLES):

    flist = []
    for project in projects:
        for sample in sample_dict[project]:
            if tool_name == 'mosdepth':
                f = INTERIM_DIR / "mosdepth" / project / f"{sample}.mosdepth.global.dist.txt"
            elif tool_name == 'verifyBamID':
                f = PROCESSED_DIR / "verifyBamID" / project / f"{sample}.Ancestry"
            else:
                print (f"Unexpected tool name '{tool_name}' supplied. Exiting.")
                raise SystemExit(1)

            flist.append(f)

    return flist


##############   MODULES   ##############
include: "rules/somalier.smk"
include: "rules/indexcov.smk"
include: "rules/covviz.smk"
include: "rules/mosdepth.smk"
include: "rules/within_species_contamintation.smk"
#########################################


############   CONSTRAINTS   ############
wildcard_constraints:
    sample = '|'.join([item for sublist in SAMPLES.values() for item in sublist]),
    project = '|'.join(PROJECT_NAMES),
#########################################


rule all:
    input:
        # somalier
        expand(str(PROCESSED_DIR / "somalier/{project}/relatedness/somalier.html"),
            project=PROJECT_NAMES),
        expand(str(PROCESSED_DIR / "somalier/{project}/ancestry/somalier.somalier-ancestry.html"),
            project=PROJECT_NAMES),
        # indexcov
        expand(str(PROCESSED_DIR / "indexcov/{project}/index.html"),
            project=PROJECT_NAMES),
        # # covviz
        # expand(str(PROCESSED_DIR / "covviz/{project}/covviz_report.html"),
        #     project=PROJECT_NAMES),
        # # mosdepth
        get_targets('mosdepth'),
        expand(str(PROCESSED_DIR / "mosdepth/{project}/mosdepth_{project}.html"),
            project=PROJECT_NAMES),
        #### within-species contamination QC ####
        # verifyBamID
        get_targets('verifyBamID'),


